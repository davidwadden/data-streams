buildscript {
    ext {
        springBootVersion = '1.5.0.RC1'
        junitPluginVersion = '1.0.0-M3'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.junit.platform:junit-platform-gradle-plugin:${junitPluginVersion}")
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'org.junit.platform.gradle.plugin'

    ext {
        junitVersion = '5.0.0-M3'
        assertJVersion = '3.6.1'
        springCloudVersion = 'Camden.SR4'
        springCloudStreamVersion = '1.2.0.M1'
        springIntegrationKafkaVersion = '2.1.0.RELEASE'
        springIntegrationVersion = '5.0.0.M2'
        mockitoVersion = '2.+'
        kafkaVersion = '0.10.1.1'
        avroVersion = '1.8.1'
        jsonAssertVersion = '1.4.0'
        dozerVersion = '5.5.1'
        reactorVersion = '3.0.4.RELEASE'
    }
    ext['spring.version'] = '5.0.0.M4'
    ext['spring-cloud-stream.version'] = 'Brooklyn.SR1'
    ext['kafka.version'] = '0.10.1.1'
    ext['spring-integration-kafka.version'] = '2.1.0.RELEASE'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }

    dependencies {
        compileOnly('org.projectlombok:lombok')

        testCompile("org.junit.jupiter:junit-jupiter-api")
        testRuntime("org.junit.jupiter:junit-jupiter-engine")
        // see: https://github.com/junit-team/junit5/issues/586#issuecomment-265012600
        testCompile("org.junit.platform:junit-platform-launcher")
        testCompile("org.mockito:mockito-core")
        testCompile("org.assertj:assertj-core")
        testCompileOnly('org.projectlombok:lombok')
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }

        dependencies {
            dependencySet(group: 'org.springframework.cloud', version: "${springCloudStreamVersion}".toString()) {
                entry 'spring-cloud-starter-stream-kafka'
                entry 'spring-cloud-stream-binder-kafka'
                entry 'spring-cloud-stream'
                entry 'spring-cloud-stream-reactive'
                entry 'spring-cloud-stream-codec'
                entry 'spring-cloud-stream-test-support'
            }

            dependency "org.springframework.integration:spring-integration-kafka:${springIntegrationKafkaVersion}"
            dependency "org.springframework.integration:spring-integration-core:${springIntegrationVersion}"
            dependency "org.springframework.integration:spring-integration-test:${springIntegrationVersion}"

            dependency "org.apache.kafka:kafka-clients:${kafkaVersion}"
            dependency "org.apache.kafka:kafka_2.11:${kafkaVersion}"
            dependency "org.apache.avro:avro:${avroVersion}"
            dependency "net.sf.dozer:dozer:${dozerVersion}"
            dependency "io.projectreactor:reactor-core:${reactorVersion}"

            dependency "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
            dependency "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
            dependency "org.junit.platform:junit-platform-launcher:${junitPluginVersion}"
            dependency "org.mockito:mockito-core:${mockitoVersion}"
            dependency "org.assertj:assertj-core:${assertJVersion}"
            dependency "org.skyscreamer:jsonassert:${jsonAssertVersion}"
            dependency "io.projectreactor.addons:reactor-test:${reactorVersion}"
        }
    }

    configurations.all {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'log4j', module: 'log4j'
    }

}

task bootRunAll(dependsOn: [':http-source:bootRun', ':http-api-sink:bootRun', ':fakes:bootRun']) {}
