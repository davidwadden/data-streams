buildscript {
    ext {
        springBootVersion = '1.5.0.BUILD-SNAPSHOT'
        junitPluginVersion = '1.0.0-M3'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.junit.platform:junit-platform-gradle-plugin:${junitPluginVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.junit.platform.gradle.plugin'

ext {
    junitVersion = '5.0.0-M3'
    assertJVersion = '3.6.1'
    springCloudVersion = 'Camden.SR2'
    mockitoVersion = '2.+'
    kafkaVersion = '0.10.1.0'
}
ext['spring.version'] = '5.0.0.M3'
ext['spring-cloud-stream.version'] = 'Brooklyn.SR1'

jar {
    baseName = 'streams'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}


dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.cloud:spring-cloud-starter-stream-kafka')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.apache.kafka:kafka-clients:${kafkaVersion}")
    compile("org.apache.kafka:kafka_2.11:${kafkaVersion}")
    compileOnly('org.projectlombok:lombok')

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testCompile('org.springframework.integration:spring-integration-test')
    testCompile('org.springframework.cloud:spring-cloud-stream-test-support')
    testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    // see: https://github.com/junit-team/junit5/issues/586#issuecomment-265012600
    testCompile("org.junit.platform:junit-platform-launcher:${junitPluginVersion}")
    testCompile("org.mockito:mockito-core:${mockitoVersion}")
    testCompile("org.assertj:assertj-core:${assertJVersion}")
    testCompileOnly('org.projectlombok:lombok')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

configurations.all {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'log4j', module: 'log4j'
}
