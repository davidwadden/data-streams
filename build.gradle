buildscript {
    ext {
        springBootVersion = '1.5.1.RELEASE'
        junitPluginVersion = '1.0.0-M3'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/release" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.junit.platform:junit-platform-gradle-plugin:${junitPluginVersion}")
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'org.junit.platform.gradle.plugin'

    ext {
        springCloudVersion = 'Dalston.M1'
        springIntegrationVersion = '5.0.0.M2'
        springIntegrationKafkaVersion = '2.1.0.RELEASE'
        reactorVersion = '3.0.4.RELEASE'
        dozerVersion = '5.5.1'
        avroVersion = '1.8.1'
        kafkaVersion = '0.10.1.1'

        junitVersion = '5.0.0-M3'
        mockitoVersion = '2.+'
        assertJVersion = '3.6.1'
        jsonAssertVersion = '1.4.0'
        jsonUnitVersion = '1.18.0'
    }
    ext['spring.version'] = '5.0.0.M4'
    ext['kafka.version'] = '0.10.1.1'
    ext['spring-integration-kafka.version'] = '2.1.0.RELEASE'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/release" }
        maven { url "https://repo.spring.io/milestone" }
    }

    dependencies {
        compileOnly('org.projectlombok:lombok')

        testCompile("org.junit.jupiter:junit-jupiter-api")
        testRuntime("org.junit.jupiter:junit-jupiter-engine")
        testCompile("org.mockito:mockito-core")
        testCompile("org.assertj:assertj-core")
        testCompileOnly('org.projectlombok:lombok')
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "io.projectreactor:reactor-bom:Aluminium-RELEASE"
        }

        dependencies {
            dependency "org.springframework.integration:spring-integration-kafka:${springIntegrationKafkaVersion}"
            dependency "org.springframework.integration:spring-integration-core:${springIntegrationVersion}"
            dependency "org.springframework.integration:spring-integration-test:${springIntegrationVersion}"

            dependency "org.apache.kafka:kafka-clients:${kafkaVersion}"
            dependency "org.apache.kafka:kafka_2.11:${kafkaVersion}"
            dependency "org.apache.avro:avro:${avroVersion}"
            dependency "net.sf.dozer:dozer:${dozerVersion}"

            dependencySet(group: 'net.javacrumbs.json-unit', version: "${jsonUnitVersion}".toString()) {
                entry 'json-unit'
                entry 'json-unit-fluent'
                entry 'json-unit-spring'
            }

            dependency "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
            dependency "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
            dependency "org.mockito:mockito-core:${mockitoVersion}"
            dependency "org.assertj:assertj-core:${assertJVersion}"
            dependency "org.skyscreamer:jsonassert:${jsonAssertVersion}"
        }
    }

    configurations.all {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'log4j', module: 'log4j'
    }

}

task bootRunAll(dependsOn: [
        ':http-source:bootRun',
        ':http-api-sink:bootRun',
        ':avro-processor:bootRun',
        ':fakes:bootRun'
]) {}
